OUTPUT_ARCH(mips)
ENTRY(kernel_entry)
SECTIONS
{
  /* Read-only sections, merged into text segment: */
  . = @@LOADADDR@@;
  .init          : { *(.init)		} =0
  .text      :
  {
    _ftext = . ;
    *(.text)     
  . = ALIGN(@@IRAMSIZE@@);
   __iram = . ;
		*(.iram-gen)		/* general usage (essential) */
		*(.iram-fwd)		/* general packet forwarding used */
		*(.iram-l2-fwd)		/* L2 packet forwarding */
		*(.iram-l34-fwd)	/* L34 packet forwarding */
		*(.iram-tx)		/* protocol stack TX */
		*(.iram-extdev)		/* ROMEDRV extension device fwd */
		*(.iram-airgo)		/* airgo pre-N driver */
		*(.iram-rtkwlan)	/* realtek wlan driver */
		*(.iram-crypto)		/* authetication / crypto-engine */
		*(.iram-voip)		/* voip */
		*(.iram)		/* other usage */
		*(.iram.1)
		
	/* start of wlan f/w definition */		
		__fw_start = . ;
   		*(.rtl8192sfw.bin)
   		__fw_end = . ;		   		
   			
		__AGC_TAB_start = . ;
   		*(.AGC_TAB.txt)
   		__AGC_TAB_end = . ;	
   		
		__phy_reg_start = . ;
   		*(.phy_reg.txt)
   		__phy_reg_end = . ;	   		

                __phy_reg_MP_start = . ;
                *(.phy_reg_MP.txt)
                __phy_reg_MP_end = . ;

		__MACPHY_REG_start = . ;
   		*(.MACPHY_REG.txt)
   		__MACPHY_REG_end = . ;	   		
   		
		__radio_a_start = . ;
   		*(.radio_a.txt)
   		__radio_a_end = . ;	   		   		   		
   		
		__radio_b_start = . ;
   		*(.radio_b.txt)
   		__radio_b_end = . ;	   		  

		__PHY_REG_PG_start = .;
		*(.PHY_REG_PG.txt)
		__PHY_REG_PG_end = .;

		__PHY_to1T2R_start = .;
		*(.PHY_to1T2R.txt)
		__PHY_to1T2R_end = .;

                __PHY_to1T2R_b_start = .;
                *(.PHY_to1T2R_b.txt)
                __PHY_to1T2R_b_end = .;

                __PHY_to1T1R_start = .;
                *(.PHY_to1T1R.txt)
                __PHY_to1T1R_end = .;

                __PHY_to1T1R_b_start = .;
                *(.PHY_to1T1R_b.txt)
                __PHY_to1T1R_b_end = .;

		__radio_a_hp_start = . ;
   		*(.radio_a_hp.txt)
   		__radio_a_hp_end = . ;	   		   		   		

	/* end of wlan f/w definition */   		   			   		   		
   		
    *(.rodata)
    *(.rodata.*)
    *(.rodata1)
    /* .gnu.warning sections are handled specially by elf32.em.  */
    *(.gnu.warning)
  } =0
   . = ALIGN(@@DRAMSIZE@@);
   __dram = . ;
   __dram_start = . ;
   .dram	:
   {
   		*(.dram-gen)		/* general usage (essential) */
   		*(.dram-fwd)		/* general packet forwarding used */
   		*(.dram-l2-fwd)		/* L2 packet forwarding */
   		*(.dram-l34-fwd)	/* L34 packet forwarding */
   		*(.dram-extdev)		/* ROMEDRV extension device fwd */
   		*(.dram-airgo)		/* airgo pre-N driver */
   		*(.dram-rtkwlan)	/* realtek wlan driver */
		*(.dram-crypto)		/* authetication / crypto-engine */
   		*(.dram-voip)		/* voip */
		*(.dram-tx)		/* protocol stack TX */
		*(.dram)		/* other usage */
		*(.dram.1)
   }
   . = ALIGN(@@DRAMSIZE@@);
   __dram_end = . ;
  .kstrtab : { *(.kstrtab) }
/*
  .wlan_fw      :
  {
  		__fw_start = . ;
   		*(.fw_main)
   		__fw_end = . ;		
  }
*/
  . = ALIGN(16);		/* Exception table */
  __start___ex_table = .;
  __ex_table : { *(__ex_table) }
  __stop___ex_table = .;

  __start___dbe_table = .;	/* Exception table for data bus errors */
  __dbe_table : { *(__dbe_table) }
  __stop___dbe_table = .;

  __start___ksymtab = .;	/* Kernel symbol table */
  __ksymtab : { *(__ksymtab) }
  __stop___ksymtab = .;

  _etext = .;

  . = ALIGN(@@STACKSIZE@@);
  .data.init_task : { *(.data.init_task) }

  /* Startup code */
  . = ALIGN(4096);
  __init_begin = .;
  .text.init : { *(.text.init) }
  .data.init : { *(.data.init) }
  . = ALIGN(16);
  __setup_start = .;
  .setup.init : { *(.setup.init) }
  __setup_end = .;
  __initcall_start = .;
  .initcall.init : { *(.initcall.init) }
  __initcall_end = .;
  . = ALIGN(4096);	/* Align double page for init_task_union */
  __init_end = .;

  . = ALIGN(32);
  .data.cacheline_aligned : { *(.data.cacheline_aligned) }

  .fini      : { *(.fini)    } =0
  .reginfo : { *(.reginfo) }
  /* Adjust the address for the data segment.  We want to adjust up to
     the same address within the page on the next page up.  It would
     be more correct to do this:
       . = .;
     The current expression does not correctly handle the case of a
     text segment ending precisely at the end of a page; it causes the
     data segment to skip a page.  The above expression does not have
     this problem, but it will currently (2/95) cause BFD to allocate
     a single segment, combining both text and data, for this case.
     This will prevent the text segment from being shared among
     multiple executions of the program; I think that is more
     important than losing a page of the virtual address space (note
     that no actual memory is lost; the page which is skipped can not
     be referenced).  */
  . = .;
  .data    :
  {
    _fdata = . ;
    *(.data)

    *(.rodata.cst4)                                                    
    *(.rodata.str1.4)
                                                     
   /* Align the initial ramdisk image (INITRD) on page boundaries. */
   . = ALIGN(4096);
   __rd_start = .;
   *(.initrd)
   . = ALIGN(4096);
   __rd_end = .;

    CONSTRUCTORS
  }
  .data1   : { *(.data1) }
  _gp = . + 0x8000;
  .lit8 : { *(.lit8) }
  .lit4 : { *(.lit4) }
  .ctors         : { *(.ctors)   }
  .dtors         : { *(.dtors)   }
  .got           : { *(.got.plt) *(.got) }
  .dynamic       : { *(.dynamic) }
  /* We want the small data sections together, so single-instruction offsets
     can access them all, and initialized data all before uninitialized, so
     we can shorten the on-disk segment size.  */
  .sdata     : { *(.sdata) }
  . = ALIGN(4);
  _edata  =  .;
  PROVIDE (edata = .);

  __bss_start = .;
  _fbss = .;
  .sbss      : { *(.sbss) *(.scommon) }
  .bss       :
  {
   *(.dynbss)
   *(.bss*)
   *(COMMON)
   .  = ALIGN(4);
  _end = . ;
  PROVIDE (end = .);
  }

  /* Sections to be discarded */
  /DISCARD/ :
  {
        *(.text.exit)
        *(.data.exit)
        *(.exitcall.exit)
  }

  /* This is the MIPS specific mdebug section.  */
  .mdebug : { *(.mdebug) }
  /* These are needed for ELF backends which have not yet been
     converted to the new style linker.  */
  .stab 0 : { *(.stab) }
  .stabstr 0 : { *(.stabstr) }
  /* DWARF debug sections.
     Symbols in the .debug DWARF section are relative to the beginning of the
     section so we begin .debug at 0.  It's not clear yet what needs to happen
     for the others.   */
  .debug          0 : { *(.debug) }
  .debug_srcinfo  0 : { *(.debug_srcinfo) }
  .debug_aranges  0 : { *(.debug_aranges) }
  .debug_pubnames 0 : { *(.debug_pubnames) }
  .debug_sfnames  0 : { *(.debug_sfnames) }
  .line           0 : { *(.line) }
  /* These must appear regardless of  .  */
  .gptab.sdata : { *(.gptab.data) *(.gptab.sdata) }
  .gptab.sbss : { *(.gptab.bss) *(.gptab.sbss) }
  .comment : { *(.comment) }
  .note : { *(.note) }
}
